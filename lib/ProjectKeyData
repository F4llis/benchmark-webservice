PKeyData := proc(PID:string, Title:string, fnBase:string, createDat:string, 
                 isPublic:boolean, directMap:boolean, props:table)
   noeval(procname(args)):
end:
CompleteClass(PKeyData):

AddPKeyData := proc(tit:string, fnbase:string)
    global PROJECTS;
    alphnum := {seq(i,i=48..57), seq(i,i=65..90),seq(i,i=97..122)}:
    id := ConcatStrings( [seq(IntToAscii(Rand(alphnum)), 15)] );
    pr := PKeyData(id, tit, fnbase, date(), false, false, table());

    verb := Set(quiet=true);
    OpenAppending(datdir.'projectKeys.drw');
    printf('PROJECTS[%A] := %A:\n', id, pr);
    OpenAppending(previous);
    Set(quiet=verb);
    
    ReadProgram(datdir.'projectKeys.drw');
 
    return(id);
end:

GetPKeyData := proc(id:string)
    global PROJECTS;
    if not assigned(PROJECTS) or PROJECTS[id]=0 then 
        ReadProgram(datdir.'projectKeys.drw') 
    fi:

    p := PROJECTS[id];
    if p=0 then error('invalid project id') fi:
    return(p);
end:

GetPublicProjects := proc()
    global PROJECTS;
    if not assigned(PROJECTS) then ReadProgram(datdir.'projectKeys.drw') fi;

    p := [];
    for id in Indices(PROJECTS) do
        if PROJECTS[id, isPublic] then
           p := append(p, PROJECTS[id]);
        fi;
    od;
    return(p);
end:

GetProjISets := proc(prjs:list)
    global ISET:
    iSet := intersect();
    for z in prjs do 
        if type(z,PKeyData) then
            PKobj := z;
        elif type(z,string) then
            PKobj := GetPKeyData(z);
        else error('invalid project id') fi:

        ISET := 0;
        ReadProgram(PKobj['fnBase'].'.iset');
        if ISET=0 then error('could not load iSet of '.PKobj['Title']) fi:

        iSet := intersect(iSet, ISET);
    od:
    return(iSet);
end:

ProjMetProperty := proc( PID:string, Prop:TestProperty )
    pKey := GetPKeyData(PID);
    p := pKey['props'];
    nam := Prop['Name'];
    if p[nam]=unassigned or Prop['YoungerThan']>p[nam,2] then
        # recompute whether property is met
	res := Prop['Evaluator'](pKey, Prop);
	p[nam] := [res, UTCTime()];
	pKey['props'] := p:

	t := ReadRawFile(datdir.'projectKeys.drw');
	querry := 'PROJECTS['''.PID.'''] := ';
	off1 := SearchString(querry, t);
	k := SearchString(':\n',t[off1..-1]);
	t2 := ReplaceString(t[off1+1..off1+k], sprintf('%s%A:',querry, pKey),t);
	
	# store new PKeyData in file
	verb := Set(quiet=true);
	OpenWriting(datdir.'projectKeys.drw');
	prints(t2):
	OpenWriting(previous);
        Set(quiet=verb);
    fi:
    return(p[nam,1]);
end:


