#
# this script receives resDBfn, relsinfn, seqmapfn, orgMap, lock, PID
#
WriteLock(lock,'initializing computation');
#ReadProgram('/local/BenchmarkService/lib/darwinit');

GenerateProteinMapping := proc( seqMapFn:string, selOrg:list )
    global MappingComb;
    MappingComb := 0;
    ReadProgram(seqMapFn):
    if MappingComb=0 then error('could not read sequence mapping file') fi:

    mapping := []:
    for pg to length(selOrg) do if selOrg[pg]>0 then
        mapping := append(mapping, op(MappingComb[pg,selOrg[pg]]));
    fi od:
    mapping := sort(mapping);
    pset := {seq(z[2],z=mapping)};
    plist:= [op(pset)];
    mapping := transpose( [seq([z[1],z[2],SearchOrderedArray(z[2],plist)], 
                           z=mapping)]);
    return(mapping, pset);
end:

UpdateRemainingTime := proc()
    remTime := time()/relDone * (pKey['NOrthSub']-relDone);
    unit := 'sec';
    if remTime>120 then remTime := remTime/60; unit:='min'; fi:
    WriteLock(lock, sprintf(
        '%d/%d (%.1f%%) relations/groups processed. Estimated remaining time:%.0f%s\n',
	 relDone, pKey['NOrthSub'], 100*relDone/pKey['NOrthSub'], remTime, unit));
end:

MapIDtoENr := proc(id:string)
    i := SearchOrderedArray(id, mapping[1]);
    if i<=0 or i>length(mapping[1]) or mapping[1,i]<>id then
        if printlevel>1 then warning('missing id '''.id.''''); fi:
        return(NULL);
    else return(mapping[-1,i]) fi: # return the last column value, i.e. the project eNr
end:

# callback function pairwise data
PairRelations := proc(rel:list)
    global VPs, relDone, t0, relMapped, missing;
    for z in rel do
        p1 := MapIDtoENr(z[1]);
        p2 := MapIDtoENr(z[2]);
        if p1=NULL or p2=NULL then next fi:

        VPs[p1] := append(VPs[p1], p2); 
        VPs[p2] := append(VPs[p2], p1);
	relMapped := relMapped+1;
    od:
    relDone := relDone + length(rel);
    if not assigned(t0) or time()-t0>5 then 
        UpdateRemainingTime(); t0 := time(); 
    fi:
end:

ExtractInducedPairsR := proc(hcl)
    if not member(op(0,hcl), {OG,PG}) then 
        error('unexpeced argument: '.op(0,hcl)) fi:

    indOrthologs := indParalogs := membProt := [];
    for z in hcl do 
        if type(z,string) then 
            # this is a single protein. no recursion necessay.
            eNr := MapIDtoENr(z);
            if eNr<>NULL then membProt := append(membProt, [eNr]) else lprint(z,' not mapped') fi:
        elif type(z, structure) and member(op(0,z),{OG,PG}) then 
            child := procname(z);
            indOrthologs := append(indOrthologs, op(child[1]));
            indParalogs := append(indParalogs, op(child[2]));
            membProt := append( membProt, [op(indets([child], integer))] );
        else error('unexpected data: '.string(z)) fi:
    od:
    
    NSubGroup := length(membProt):
    if NSubGroup<2 then warning('dubious group') fi:
    if op(0,hcl)=OG then
        orgs := [seq([seq( GenomeNrFromENr(x1,referenceDatSet), 
            x1=subgroups)], subgroups=membProt)];
        for i to NSubGroup do for j from i+1 to NSubGroup do
            for x1 to length(membProt[i]) do for y1 to length(membProt[j]) do 
                e1 := membProt[i,x1]; e2 := membProt[j,y1];
                if e1>e2 then t := e1; e1:=e2; e2 := t; fi:
                if e1=e2 then 
                    warning('protein cannot be orthologous/paralogous to itself: '.e1);
                    next;
                fi:
                if orgs[i,x1]<>orgs[j,y1] then 
                    indOrthologs := append(indOrthologs, [e1, e2] );
                else indParalogs := append(indParalogs, [e1, e2] );
                fi:
            od od:
        od od:
    elif op(0,hcl)=PG then 
        for i to NSubGroup do for j from i+1 to NSubGroup do
            indParalogs := append( indParalogs, seq(seq(If(x<y,[x,y],[y,x]),
                y=membProt[j]), x=membProt[i]) );
        od od:
    fi:
    return( indOrthologs, indParalogs ):
end:

# callback function group data
GroupRelations := proc(og)
    global VPs, relDone, relMapped;
    t := ExtractInducedPairsR(og):
    for z in t[1] do 
        VPs[z[1]] := append(VPs[z[1]], z[2]);
        VPs[z[2]] := append(VPs[z[2]], z[1]);
    od:
    relDone := relDone + 1;
    relMapped := relMapped + 1;
    if not assigned(t0) or time()-t0>5 then 
        UpdateRemainingTime(); t0 := time(); 
    fi:
end:


WriteDB := proc(fn:string, VPs, mapping)
    verb := Set(printgc=false);
    OpenWriting(fn);
    for i to length(VPs) do
        oE := iSet[i]; 
        vps := [op({seq(iSet[z],z=VPs[i])})];
        printf('<E><OE>%d</OE><VP>%A</VP><SEQ>%s</SEQ></E>\n',
           oE, vps, Nr2SEQ(oE) );
    od:
    OpenWriting(previous);
    
    OpenWriting( fn[1..-3].'iset' );
    printf('ISET := %A:\n', If(referenceDatSet='OMA',iSet,intersect()));
    OpenWriting( previous ); 
    Set(printgc=verb);
end:

# to break at error
to 1 do
    pKey := GetPKeyData(PID);
    referenceDatSet := pKey['ref'];
    if referenceDatSet='OMA' then
        t := GenerateProteinMapping(seqmapfn, orgMap):
        mapping := t[1]; iSet := t[2]:
    else
        IDmapping := 0;
        ReadProgram( eval(symbol(lowercase(referenceDatSet).'DBpath')) . 'IDmapping.drw'):
        if IDmapping=0 then error('could not read IDmapping file'); fi:
        mapping := transpose(sort(IDmapping)):
        iSet := {seq(i,i=1..NrOfProteins(referenceDatSet))};
    fi:
    NM := length(iSet):
    VPs := CreateArray(1..NM,[]);
    relDone := relMapped := 0;
    missing := Counter('# of ignored mappings due to missing seqMapping');
    ReadProgram(relsinfn);
    WriteLock(lock, 'Storing final project data');
    WriteDB(resDBfn, VPs, mapping);
    ReadDb(resDBfn);

    pKey['NSeqMap'] := NM; pKey['NOrthMap'] := relMapped;
    orgs := {seq(GenomeCodeFromENr(z,referenceDatSet),z=iSet)}:
    pKey['OrgsCovered'] := orgs:
    if referenceDatSet<>'OMA' and relMapped>0 and 
        pKey['NOrthSub']>0 and relMapped/pKey['NOrthSub']>.01 then
        pKey['isPublic'] := true;
    fi:
    UpdatePKeyDataPermanent(pKey);

    CallSystem('rm -f '.lock);
od:
done
